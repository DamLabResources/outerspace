import pandas as pd
from pathlib import Path

WORKFLOW_DIR = Path(workflow.snakefile).parent

configfile: 'config.yaml'

SAMPLES = pd.read_csv(config['samples']).set_index('sample_name')
print(SAMPLES)

rule count_all:
    input:
        expand('counts/{sample}.csv', sample=SAMPLES.index)

rule findseq_all:
    input:
        expand('extracted/{sample}.csv', sample=SAMPLES.index)


def get_input_files(wildcards):
    sample = wildcards.sample
    return SAMPLES.loc[sample, 'reads'].split('|')

def get_toml_file(wildcards):
    sample = wildcards.sample
    return SAMPLES.loc[sample].get('toml', config['toml'])

rule findseq:
    input:
        reads = get_input_files,
        toml = get_toml_file
    output:
        'extracted/{sample}.csv'
    wrapper:
        f'file:{WORKFLOW_DIR}/wrappers/findseq'


def get_countable_file(wildcards):
    sample = wildcards.sample
    if config.get('collapse_columns', None):
        return 'condensed/{sample}.csv'.format(sample=sample)
    return 'extracted/{sample}.csv'.format(sample=sample)


rule count:
    input:
        csv = get_countable_file,
        toml = get_toml_file
    output:
        'counts/{sample}.csv'
    wrapper:
        f'file:{WORKFLOW_DIR}/wrappers/count'

rule collapse:
    input:
        'extracted/{sample}.csv',
        toml = get_toml_file
    output:
        'condensed/{sample}.csv'
    wrapper:
        f'file:{WORKFLOW_DIR}/wrappers/collapse'

